// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RVD-Projects

import RVD-Projects/Types/3
import RVD-Projects/Functions/2

import TradingView/ta/9
import PineCoders/Time/5

// @version=6
// @link RVD-Projects/VWap/1
// @description VWAP Channel Indicator
indicator("VWAP-Channel", overlay = true, behind_chart = true)

src1 = input.source(defval = low, title="S1", group="VWAP Settings")
src2 = input.source(defval = high, title="S2", group="VWAP Settings")

var anchor = input.string(defval = "Session", title="Anchor Period",
 options=["Session", "Week", "Month", "Quarter", "Year", "Decade", "Century", "Earnings", "Dividends", "Splits"], group="VWAP Settings")

new_earnings = request.earnings(syminfo.tickerid, earnings.actual, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_dividends = request.dividends(syminfo.tickerid, dividends.gross, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_split = request.splits(syminfo.tickerid, splits.denominator, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)

isNewPeriod = switch anchor
	"Earnings"  => not na(new_earnings)
	"Dividends" => not na(new_dividends)
	"Splits"    => not na(new_split)
	"Session"   => timeframe.change("D")
	"Week"      => timeframe.change("W")
	"Month"     => timeframe.change("M")
	"Quarter"   => timeframe.change("3M")
	"Year"      => timeframe.change("12M")
	"Decade"    => timeframe.change("12M") and year % 10 == 0
	"Century"   => timeframe.change("12M") and year % 100 == 0
	=> false

isEsdAnchor = anchor == "Earnings" or anchor == "Dividends" or anchor == "Splits"
if na(src1[1]) and not isEsdAnchor
	isNewPeriod := true

vwap1 = ta.vwap(src1, isNewPeriod)
vwap2 = ta.vwap(src2, isNewPeriod)

plot(vwap1, color=color.fuchsia, title="VWAP Low")
plot(vwap2, color=color.fuchsia, title="VWAP High")